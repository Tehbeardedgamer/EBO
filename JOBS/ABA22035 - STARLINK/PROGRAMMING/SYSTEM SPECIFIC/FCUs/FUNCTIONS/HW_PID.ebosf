Arg 1 ReverseActing
Arg 2 ProcessVariable
Arg 3 Setpoint
Arg 4 Integral
Arg 5 ProportionalK
Arg 6 IntegralK
Arg 7 DerivativeK
Arg 8 OldError

Numeric ThisError, Derivative,PIDOUT

If ReverseActing = 1 then
    ThisError = ProcessVariable - Setpoint
Else
    ThisError = Setpoint - ProcessVariable
Endif

' Calculate the rate of change of the error for the derivative term
Derivative = ThisError - OldError

' Update the integral term with the current error and prevent windup by limiting between 0 and 100
Integral = maximum(minimum(Integral + ThisError * IntegralK, 100), 0)

' Update the OldError for the next iteration
OldError = ThisError

' Calculate the PID output
PIDOUT = Integral + ThisError * ProportionalK + Derivative * DerivativeK

If passed(9) & passed(10) & passed(11) then
    Arg[11] = maximum(minimum(PIDOUT, 100), 0)
    Return (Arg[11] * ((Arg[10] - Arg[9]) / 100)) + Arg[9]
Else
    Return maximum(minimum(PIDOUT, 100), 0)
Endif




