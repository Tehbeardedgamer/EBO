NUMERIC INPUT HWP_DOWN_A, HWP_STATUS_A, PUMPS_REQ, PUMP_A
NUMERIC INPUT HWP_MASTER_SPEED_CMD, PUMPS_RUNNING, PUMPS_AVAIL
NUMERIC OUTPUT HWP_SPEED_CMD_A, HWP_SS_A
NUMERIC PUBLIC HWP_FAILED_A
Datetime OUTPUT HWP_LASTSTART_A, HWP_LASTSTOP_A
NUMERIC TIMER
Function SECTIMER


GOTO INITALIZE

INITALIZE:
HWP_FAILED_A = 0
IF (HWP_STATUS_A = On)  THEN GOTO PUMP_ON ELSE GOTO PUMP_OFF

PUMP_OFF:

  IF (PUMP_A <= PUMPS_REQ) & (PUMP_A > 0) & (HWP_DOWN_A = Off)  THEN
    HWP_LASTSTART_A = Date
    HWP_SPEED_CMD_A = 35
    GOTO PUMP_START
  EndIF
  HWP_SS_A = Off
  HWP_SPEED_CMD_A = 0

PUMP_START:
  IF ((PUMP_A > PUMPS_REQ) ! (PUMP_A = 0) ! (HWP_DOWN_A = On)) & (PUMPS_RUNNING > PUMPS_REQ)  THEN GOTO RAMP_DOWN
  IF (TS >= 5) & (HWP_STATUS_A = On)  THEN GOTO RAMP_UP
  IF (TS >= 30)  THEN
    HWP_FAILED_A = On
    GOTO PUMP_FAILED 
  EndIF
  HWP_SS_A = On

RAMP_UP:
  IF ((PUMP_A > PUMPS_REQ) ! (PUMP_A = 0) ! (HWP_DOWN_A = On)) & (PUMPS_RUNNING > PUMPS_REQ)  THEN GOTO RAMP_DOWN
  IF (HWP_SPEED_CMD_A >= (HWP_MASTER_SPEED_CMD - 2))  THEN GOTO PUMP_ON
  HWP_SPEED_CMD_A = minimum(HWP_SPEED_CMD_A + 5, 100)
  GOTO RAMP_UP_WAIT

RAMP_UP_WAIT:
  IF (HWP_STATUS_A = Off)  THEN GOTO PUMP_START
  IF (TS >= 5)  THEN GOTO RAMP_UP

PUMP_ON:
  IF ((PUMP_A > PUMPS_REQ) ! (PUMP_A = 0) ! (HWP_DOWN_A = On)) & (PUMPS_RUNNING > PUMPS_REQ)  THEN GOTO RAMP_DOWN_WAIT
  IF SECTIMER(HWP_STATUS_A = Off, 10, TIMER)  THEN
    HWP_FAILED_A = On
    GOTO PUMP_FAILED
  EndIF
  HWP_SS_A = On
  HWP_SPEED_CMD_A = HWP_MASTER_SPEED_CMD

RAMP_DOWN:
  IF (PUMP_A <= PUMPS_REQ) & (PUMP_A > 0) & (HWP_DOWN_A = Off) THEN GOTO RAMP_UP
  IF (HWP_SPEED_CMD_A <= 10) ! (HWP_STATUS_A = Off) ! (PUMPS_REQ = 0)  THEN
    HWP_LASTSTOP_A = Date
    GOTO PUMP_OFF
  EndIF
  HWP_SPEED_CMD_A = maximum(HWP_SPEED_CMD_A - 1, 10)
  GOTO RAMP_DOWN_WAIT

RAMP_DOWN_WAIT:
  IF (TS >= 5)  THEN GOTO RAMP_DOWN

PUMP_FAILED:
  IF (PUMPS_AVAIL = 0)  THEN HWP_FAILED_A = Off
  IF (HWP_FAILED_A = Off)  THEN GOTO PUMP_OFF
  HWP_SS_A = Off
  HWP_SPEED_CMD_A = 0