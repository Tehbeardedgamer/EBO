NUMERIC INPUT PUMP_DOWN_2, PUMP_STATUS_2, PUMPS_REQ,STAGE
NUMERIC INPUT CHWP_MASTER_SPEED_CMD_2,PUMPS_AVAIL
NUMERIC OUTPUT PUMP_SPEED_CMD_2, PUMP_SS_2
NUMERIC PUBLIC PUMP_FAILED_2

NUMERIC TIMER
Function SECTIMER

IF (PUMP_STATUS_2 = On)  THEN GOTO PUMP_ON ELSE GOTO PUMP_OFF

PUMP_OFF:
  IF (STAGE = 1) & (PUMP_DOWN_2 = Off)  THEN ' IF P2 is down for maint then i need to ensure pump A takes over until p2 is back up
    PUMP_SPEED_CMD_2 = 35
    GOTO PUMP_START
  EndIF
  
  PUMP_SS_2 = Off
  PUMP_SPEED_CMD_2 = 0

PUMP_START:
  IF ((STAGE = 2) ! (PUMP_DOWN_2 = On)) THEN GOTO RAMP_DOWN
  IF (TS >= 5) & (PUMP_STATUS_2 = On)  THEN GOTO RAMP_UP
  IF (TS >= 30)  THEN
    PUMP_FAILED_2 = On
    GOTO PUMP_FAILED 
  EndIF
  PUMP_SS_2 = On

RAMP_UP:
  IF ((STAGE = 2) ! (PUMP_DOWN_2 = On)) THEN GOTO RAMP_DOWN
  IF (PUMP_SPEED_CMD_2 >= (CHWP_MASTER_SPEED_CMD_2 - 2))  THEN GOTO PUMP_ON
  PUMP_SPEED_CMD_2 = minimum(PUMP_SPEED_CMD_2 + 5, 100)
  GOTO RAMP_UP_WAIT

RAMP_UP_WAIT:
  IF (PUMP_STATUS_2 = Off)  THEN GOTO PUMP_START
  IF (TS >= 5)  THEN GOTO RAMP_UP

PUMP_ON:
  IF ((STAGE = 2) ! (PUMP_DOWN_2 = On)) THEN GOTO RAMP_DOWN_WAIT
  IF SECTIMER(PUMP_STATUS_2 = Off, 10, TIMER)  THEN
    PUMP_FAILED_2 = On
    GOTO PUMP_FAILED
  EndIF
  PUMP_SS_2 = On
  PUMP_SPEED_CMD_2 = CHWP_MASTER_SPEED_CMD_2

RAMP_DOWN:
  IF (STAGE = 1 ) & (PUMP_DOWN_2 = Off) THEN GOTO RAMP_UP
  IF (PUMP_SPEED_CMD_2 <= 35) ! (PUMP_STATUS_2 = Off) ! (PUMPS_REQ = 0)  THEN
    
    GOTO PUMP_OFF
  EndIF
  PUMP_SPEED_CMD_2 = maximum(PUMP_SPEED_CMD_2 - 5, 35)
  GOTO RAMP_DOWN_WAIT

RAMP_DOWN_WAIT:
  IF (TS >= 5)  THEN GOTO RAMP_DOWN

PUMP_FAILED:
  IF (PUMPS_AVAIL = 0)  THEN PUMP_FAILED_2 = Off
  IF (PUMP_FAILED_2 = Off)  THEN GOTO PUMP_OFF
  PUMP_SS_2 = Off
  PUMP_SPEED_CMD_2 = 0