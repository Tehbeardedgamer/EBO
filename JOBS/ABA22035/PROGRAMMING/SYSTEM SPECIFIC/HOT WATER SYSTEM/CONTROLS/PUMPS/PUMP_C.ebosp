NUMERIC INPUT HWP_DOWN_C, HWP_STATUS_C, PUMPS_REQ, PUMP_C
NUMERIC INPUT HWP_MASTER_SPEED_CMD, PUMPS_RUNNING, PUMPS_CVAIL
NUMERIC OUTPUT HWP_SPEED_CMD_C, HWP_SS_C
NUMERIC PUBLIC HWP_FAILED_C
Datetime OUTPUT HWP_LASTSTART_C, HWP_LASTSTOP_C
NUMERIC TIMER
Function SECTIMER


GOTO INITALIZE

INITALIZE:
HWP_FAILED_C = 0
IF (HWP_STATUS_C = On)  THEN GOTO PUMP_ON ELSE GOTO PUMP_OFF

PUMP_OFF:

  IF (PUMP_C <= PUMPS_REQ) & (PUMP_C > 0) & (HWP_DOWN_C = Off)  THEN
    HWP_LASTSTART_C = Date
    HWP_SPEED_CMD_C = 35
    GOTO PUMP_START
  EndIF
  HWP_SS_C = Off
  HWP_SPEED_CMD_C = 0

PUMP_START:
  IF ((PUMP_C > PUMPS_REQ) ! (PUMP_C = 0) ! (HWP_DOWN_C = On)) & (PUMPS_RUNNING > PUMPS_REQ)  THEN GOTO RAMP_DOWN
  IF (TS >= 5) & (HWP_STATUS_C = On)  THEN GOTO RAMP_UP
  IF (TS >= 30)  THEN
    HWP_FAILED_C = On
    GOTO PUMP_FAILED 
  EndIF
  HWP_SS_C = On

RAMP_UP:
  IF ((PUMP_C > PUMPS_REQ) ! (PUMP_C = 0) ! (HWP_DOWN_C = On)) & (PUMPS_RUNNING > PUMPS_REQ)  THEN GOTO RAMP_DOWN
  IF (HWP_SPEED_CMD_C >= (HWP_MASTER_SPEED_CMD - 2))  THEN GOTO PUMP_ON
  HWP_SPEED_CMD_C = minimum(HWP_SPEED_CMD_C + 5, 100)
  GOTO RAMP_UP_WAIT

RAMP_UP_WAIT:
  IF (HWP_STATUS_C = Off)  THEN GOTO PUMP_START
  IF (TS >= 5)  THEN GOTO RAMP_UP

PUMP_ON:
  IF ((PUMP_C > PUMPS_REQ) ! (PUMP_C = 0) ! (HWP_DOWN_C = On)) & (PUMPS_RUNNING > PUMPS_REQ)  THEN GOTO RAMP_DOWN_WAIT
  IF SECTIMER(HWP_STATUS_C = Off, 10, TIMER)  THEN
    HWP_FAILED_C = On
    GOTO PUMP_FAILED
  EndIF
  HWP_SS_C = On
  HWP_SPEED_CMD_C = HWP_MASTER_SPEED_CMD

RAMP_DOWN:
  IF (PUMP_C <= PUMPS_REQ) & (PUMP_C > 0) & (HWP_DOWN_C = Off) THEN GOTO RAMP_UP
  IF (HWP_SPEED_CMD_C <= 10) ! (HWP_STATUS_C = Off) ! (PUMPS_REQ = 0)  THEN
    HWP_LASTSTOP_C = Date
    GOTO PUMP_OFF
  EndIF
  HWP_SPEED_CMD_C = maximum(HWP_SPEED_CMD_C - 1, 10)
  GOTO RAMP_DOWN_WAIT

RAMP_DOWN_WAIT:
  IF (TS >= 5)  THEN GOTO RAMP_DOWN

PUMP_FAILED:
  IF (PUMPS_CVAIL = 0)  THEN HWP_FAILED_C = Off
  IF (HWP_FAILED_C = Off)  THEN GOTO PUMP_OFF
  HWP_SS_C = Off
  HWP_SPEED_CMD_C = 0