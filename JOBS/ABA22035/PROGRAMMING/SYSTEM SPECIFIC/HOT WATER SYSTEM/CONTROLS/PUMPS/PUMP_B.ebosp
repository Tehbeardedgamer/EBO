NUMERIC INPUT HWP_DOWN_B, HWP_STATUS_B, PUMPS_REQ, PUMP_B
NUMERIC INPUT HWP_MASTER_SPEED_CMD, PUMPS_RUNNING, PUMPS_BVAIL
NUMERIC OUTPUT HWP_SPEED_CMD_B, HWP_SS_B
NUMERIC PUBLIC HWP_FAILED_B
Datetime OUTPUT HWP_LASTSTART_B, HWP_LASTSTOP_B
NUMERIC TIMER
Function SECTIMER


GOTO INITALIZE

INITALIZE:
HWP_FAILED_B = 0
IF (HWP_STATUS_B = On)  THEN GOTO PUMP_ON ELSE GOTO PUMP_OFF

PUMP_OFF:

  IF (PUMP_B <= PUMPS_REQ) & (PUMP_B > 0) & (HWP_DOWN_B = Off)  THEN
    HWP_LASTSTART_B = Date
    HWP_SPEED_CMD_B = 35
    GOTO PUMP_START
  EndIF
  HWP_SS_B = Off
  HWP_SPEED_CMD_B = 0

PUMP_START:
  IF ((PUMP_B > PUMPS_REQ) ! (PUMP_B = 0) ! (HWP_DOWN_B = On)) & (PUMPS_RUNNING > PUMPS_REQ)  THEN GOTO RAMP_DOWN
  IF (TS >= 5) & (HWP_STATUS_B = On)  THEN GOTO RAMP_UP
  IF (TS >= 30)  THEN
    HWP_FAILED_B = On
    GOTO PUMP_FAILED 
  EndIF
  HWP_SS_B = On

RAMP_UP:
  IF ((PUMP_B > PUMPS_REQ) ! (PUMP_B = 0) ! (HWP_DOWN_B = On)) & (PUMPS_RUNNING > PUMPS_REQ)  THEN GOTO RAMP_DOWN
  IF (HWP_SPEED_CMD_B >= (HWP_MASTER_SPEED_CMD - 2))  THEN GOTO PUMP_ON
  HWP_SPEED_CMD_B = minimum(HWP_SPEED_CMD_B + 5, 100)
  GOTO RAMP_UP_WAIT

RAMP_UP_WAIT:
  IF (HWP_STATUS_B = Off)  THEN GOTO PUMP_START
  IF (TS >= 5)  THEN GOTO RAMP_UP

PUMP_ON:
  IF ((PUMP_B > PUMPS_REQ) ! (PUMP_B = 0) ! (HWP_DOWN_B = On)) & (PUMPS_RUNNING > PUMPS_REQ)  THEN GOTO RAMP_DOWN_WAIT
  IF SECTIMER(HWP_STATUS_B = Off, 10, TIMER)  THEN
    HWP_FAILED_B = On
    GOTO PUMP_FAILED
  EndIF
  HWP_SS_B = On
  HWP_SPEED_CMD_B = HWP_MASTER_SPEED_CMD

RAMP_DOWN:
  IF (PUMP_B <= PUMPS_REQ) & (PUMP_B > 0) & (HWP_DOWN_B = Off) THEN GOTO RAMP_UP
  IF (HWP_SPEED_CMD_B <= 10) ! (HWP_STATUS_B = Off) ! (PUMPS_REQ = 0)  THEN
    HWP_LASTSTOP_B = Date
    GOTO PUMP_OFF
  EndIF
  HWP_SPEED_CMD_B = maximum(HWP_SPEED_CMD_B - 1, 10)
  GOTO RAMP_DOWN_WAIT

RAMP_DOWN_WAIT:
  IF (TS >= 5)  THEN GOTO RAMP_DOWN

PUMP_FAILED:
  IF (PUMPS_BVAIL = 0)  THEN HWP_FAILED_B = Off
  IF (HWP_FAILED_B = Off)  THEN GOTO PUMP_OFF
  HWP_SS_B = Off
  HWP_SPEED_CMD_B = 0